# Salt Auto Scaling FloScript

house autoscale

init salt.pool.m1 to mid "alpha" status on 
init salt.pool.m2 to mid "ms-0" status on 
init salt.pool.m3 to mid "ms-1" status on 
init salt.pool.m4 to mid "ms-2" status off 
init salt.pool.m5 to mid "ms-3" status off 
init salt.pool.m6 to mid "ms-4" status off 


init salt.autoscale.limit to up 0.2 down 0.19

framer eventer be active first start
   frame start
      do salt eventer job

framer overloader be active first start at 1.0
   frame start
      do salt bosser pool numcpu
      do salt bosser pool loadavg
      do salt pooler overload
   
framer autoscaler be active first startup at 1.0
   frame safety
      do salt bosser pool ping
      go abort if .time >= 60
   
      frame startup in safety # wait 6 seconds to verify all on poolees are healthy
         set elapsed to 6
         go compute if healthy in .salt.status
         go abort if elapsed >= goal and not healthy in .salt.status
      
      frame autoscale in safety
         go abort if not healthy in .salt.status
   
      frame compute in autoscale
         go scaleup if overload in .salt.status >= up in .salt.autoscale.limit
         go scaledown if overload in .salt.status <= down in .salt.autoscale.limit
         
      frame scaleup in autoscale
         #put on into ms_1 in .salt.pool.status
         go wait if always
         
      frame scaledown in autoscale
         #put off into ms_1 in .salt.pool.status
         go wait if always
      
      frame wait in autoscale
         go compute if elapsed >= 3
      
   frame abort
      bid stop all

logger logger to ../log/ flush 1
   log autoscale to autoscale on update
      loggee status salt.status
      
   log pool to pool on change
      loggee m1 salt.pool.m1
      loggee m2 salt.pool.m2
      loggee m3 salt.pool.m3
      loggee m4 salt.pool.m4
      loggee m5 salt.pool.m5
      loggee m6 salt.pool.m6
      
   log event to event on fifo
      loggee event salt.eventer.job.event