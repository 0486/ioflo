# Salt Auto Scaling FloScript

house autoscale

init salt.pool.mid to alpha "alpha" ms_0 "ms-0" ms_1 "ms-1" ms_2 "ms-2" \
      ms_3 "ms-3" ms_4 "ms-4" 
init salt.pool.status to alpha on ms_0 on ms_1 on ms_2 off ms_3 off ms_4 off 
init salt.autoscale.limit to up 0.2 down 0.19

framer eventer be active first event
   frame event
      do salt eventer job

framer overloader be active first overload at 1.0
   frame overload
      do salt bosser overload
   
framer autoscaler be active first compute at 1.0
   frame failure
      go abort if .time >= 20
      
   frame compute in failure
      go scaleup if .salt.overload >= up in .salt.autoscale.limit
      go scaledown if .salt.overload <= down in .salt.autoscale.limit
      
   frame scaleup in failure
      put off into ms_1 in .salt.pool.status
      go wait if always
      
   frame scaledown in failure
      put on into ms_1 in .salt.pool.status
      go wait if always
   
   frame wait in failure
      go compute if elapsed >= 3
      
   frame abort
      bid stop all

logger logger to ../log/
log autoscale to autoscale on update
loggee overload salt.overload
